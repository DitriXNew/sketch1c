//
//---
// Если ТекущиеДанные.Тип = Контейнер, тогда добавить в него. А если Не контейнер, то добавить рядом, но только то что доступно родителю!
//
//---
// ТипыЗначение ПоляФормы с видом ПолеВвода:
//   * Строка
//   * Число
//   * Булево
//   * Дата
//   * СтандартныйПериод

&НаКлиенте
Перем МенеджерПроекта;

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СоздатьНовыйПроект();
	ОпределитьВидимостьПодсказокКоманд(Ложь);
	СкрытьВсеСтраницыКромеТекущей();
	ОбработатьИзменениеНастроекПроекта();
	Если ЗагружатьСвойстваИзФайла Тогда
		ВыполнитьЗагрузкуСвойствИзФайла();
		
	Иначе
		ВыполнитьЗагрузкуСвойствИзМакета();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйПроект() 
	МенеджерПроекта = ПолучитьФорму("ВнешняяОбработка.Эскиз.Форма.Проект");
	МенеджерПроекта.Создать();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроект(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПроектЗавершение", ЭтотОбъект);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = "Файл проекта(*.json)|*.json";
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроектЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Ложь
		Или ВыбранныеФайлы = Неопределено
		Или ВыбранныеФайлы.Количество() = 0
		Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Файл проекта не выбран.";
		Сообщение.Сообщить();	
		
		Возврат;
	КонецЕсли;
	
	РасположениеПроекта = ВыбранныеФайлы[0];
	
	Проект = ДесериализоватьИзJSON(РасположениеПроекта, Истина);
	МенеджерПроекта.ЗагрузитьДанныеПроекта(Проект.ДанныеПроекта);
	ФормыИЭлементыУправленияИзМассиваРекурсивно(Проект.ФормыИЭлементыУправления, ФормыИЭлементыУправления);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормыИЭлементыУправленияИзМассиваРекурсивно(Строки, Родитель) 
	Для Каждого Строка Из Строки Цикл
		НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		Если НоваяСтрока.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
			МенеджерПроекта.ОткрытьПрототип(НоваяСтрока, ЭтаФорма, Ложь);
			
		Иначе
			Если НоваяСтрока.Тип = МенеджерПроекта.Типы().ГруппаФормы Тогда
				Связать = Ложь;
				Ключи = "Имя,Тип,Вид,Родитель,Прототип";

			ИначеЕсли НоваяСтрока.Тип = МенеджерПроекта.Типы().ПолеФормы Тогда
				Связать = Истина;
				Ключи = "Имя,Тип,Вид,Родитель,Прототип,Путь,ПутьКДанным";
				
			ИначеЕсли НоваяСтрока.Тип = МенеджерПроекта.Типы().КнопкаФормы Тогда
				Связать = Истина;
				Ключи = "Имя,Тип,Вид,Родитель,Прототип";
				
			КонецЕсли;
			
			ДополнитьКлючиСвойств(Ключи, НоваяСтрока);
			СвойстваЭлементаУправления = Новый Структура(Ключи);
			ЗаполнитьЗначенияСвойств(СвойстваЭлементаУправления, НоваяСтрока);
			
			МенеджерПроекта.ДобавитьЭлементУправления(СвойстваЭлементаУправления, Связать, Ложь);
			
		КонецЕсли;
		
		ДочерниеСтроки = Строка.Строки;
		Если ДочерниеСтроки.Количество() Тогда
			ФормыИЭлементыУправленияИзМассиваРекурсивно(ДочерниеСтроки, НоваяСтрока);
			
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ФормыИЭлементыУправления.ТекущаяСтрока = ФормыИЭлементыУправления.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
	Элементы.ФормыИЭлементыУправления.Развернуть(ФормыИЭлементыУправления.ПолучитьЭлементы()[0].ПолучитьИдентификатор(), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПроект(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьПроектЗавершение", ЭтотОбъект);
	НачатьЗапускПриложения(ОписаниеОповещения, РасположениеПроекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПроектЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	//	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПроект(Команда)
	Если ПустаяСтрока(РасположениеПроекта) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьПроектЗавершение", ЭтотОбъект);
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.Расширение = "json";
		ДиалогВыбораФайла.Фильтр = "json|*.json";
		ДиалогВыбораФайла.Показать(ОписаниеОповещения);
		
	Иначе
		Адрес = СохранитьПроектНаСервере();
		НачатьПолучениеФайлаССервера(, Адрес, РасположениеПроекта);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПроектЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Ложь
		Или ВыбранныеФайлы = Неопределено
		Или ВыбранныеФайлы.Количество() = 0
		Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Файл проекта не выбран.";
		Сообщение.Сообщить();	
		
		Возврат;
	КонецЕсли;
	
	РасположениеПроекта = ВыбранныеФайлы[0];
	
	Адрес = СохранитьПроектНаСервере();
	НачатьПолучениеФайлаССервера(, Адрес, РасположениеПроекта);
	
КонецПроцедуры

&НаСервере
Функция СохранитьПроектНаСервере() 
	Результат = "";
	
	ФормыИЭлементыУправленияНаСервере = РеквизитФормыВЗначение("ФормыИЭлементыУправления");
	
	Ключи = "Строки";
	Для Каждого Колонка Из ФормыИЭлементыУправленияНаСервере.Колонки Цикл
		Ключи = Ключи + "," + Колонка.Имя;
		
	КонецЦикла;
	
	Проект = Новый Структура;
	
	ДанныеПроекта = МенеджерПроекта.ПолучитьДанныеПроекта();
	Проект.Вставить("ДанныеПроекта", ДанныеПроекта);
	
	ФормыИЭлементыУправленияМассив = ФормыИЭлементыУправленияВМассивРекурсивно(ФормыИЭлементыУправленияНаСервере.Строки, Ключи);
	Проект.Вставить("ФормыИЭлементыУправления", ФормыИЭлементыУправленияМассив);
	
	Json = СериализоватьВJSON(Проект);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	Писатель = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаТекста.UTF8);
	Писатель.Записать(Json);
	Писатель.Закрыть();
	
	Файл = Новый Файл(ИмяВременногоФайла);
	Если Файл.Существует() Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		Результат = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		УдалитьФайлы(ИмяВременногоФайла);
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ФормыИЭлементыУправленияВМассивРекурсивно(Строки, Ключи)
	Результат = Новый Массив;
	
	Для Каждого Строка Из Строки Цикл
		ДанныеСтроки = Новый Структура(Ключи);
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Строка);
		
		ДочерниеСтроки = Строка.Строки;
		Если ДочерниеСтроки.Количество() Тогда
			ДанныеСтроки.Строки = ФормыИЭлементыУправленияВМассивРекурсивно(ДочерниеСтроки, Ключи);		
			
		Иначе
			ДанныеСтроки.Строки = Новый Массив;
			
		КонецЕсли;
		
		Результат.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте                                         
Процедура РасположениеПроектаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(РасположениеПроекта) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РасположениеПроектаОчисткаЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Поле ""Проект"" будет очищено! Продолжить?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет, "Эскиз", КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура РасположениеПроектаОчисткаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		РасположениеПроекта = "";
		
		СоздатьНовыйПроект();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоФормИЭлементов() 
	
КонецПроцедуры

//---

&НаКлиенте
Процедура ДобавитьФорму(Команда)
	НоваяСтрока = ФормыИЭлементыУправления.ПолучитьЭлементы().Добавить();
	НоваяСтрока.ИндексКартинки = 0;
	
	НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Форма");
	НоваяСтрока.Имя = НовоеИмя;
	НоваяСтрока.Заголовок = НовоеИмя;
	НоваяСтрока.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения;
	НоваяСтрока.Идентификатор = Новый УникальныйИдентификатор;
	
	МенеджерПроекта.ОткрытьПрототип(НоваяСтрока, ЭтаФорма, Ложь);
	
	Элементы.ФормыИЭлементыУправления.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрототип(Команда)
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокуПрототипа = ПолучитьСтрокуПрототипаРекурсивно(ТекущиеДанные);
	
	СвойстваФормы = Новый Структура("Имя,Заголовок,Идентификатор");
	ЗаполнитьЗначенияСвойств(СвойстваФормы, СтрокуПрототипа); 
	
	МенеджерПроекта.ОткрытьПрототип(СвойстваФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппу(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().ОбычнаяГруппа);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКоманднуюПанель(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().КоманднаяПанель);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодменю(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().Подменю);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницы(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().Страницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().Страница);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКнопку(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().КнопкаФормы, МенеджерПроекта.Виды().ОбычнаяКнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКнопкуКоманднойПанели(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().КнопкаФормы, МенеджерПроекта.Виды().КнопкаКоманднойПанели);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГиперссылку(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().КнопкаФормы, МенеджерПроекта.Виды().Гиперссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГиперссылкуКоманднойПанели(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().КнопкаФормы, МенеджерПроекта.Виды().ГиперссылкаКоманднойПанели);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоле(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеВвода);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФлажок(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеФлажка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПереключатель(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеПереключателя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТаблицу(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ТаблицаФормы,, МенеджерПроекта.ТипыЗначения().ТаблицаЗначений);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДерево(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ТаблицаФормы,, МенеджерПроекта.ТипыЗначения().ДеревоЗначений);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонку(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеВвода,, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппуКолонок(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().ГруппаКолонок);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКонтекстноеМеню(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().КонтекстноеМеню);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНадпись(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ДекорацияФормы, МенеджерПроекта.Виды().Надпись);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКартинку(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ДекорацияФормы, МенеджерПроекта.Виды().Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИндикатор(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеИндикатора);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолосуРегулирования(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеПолосыРегулирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКалендарь(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеКалендаря);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФорматированныйДокумент(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеФорматированногоДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФорматированнуюСтроку(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеНадписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТекстовыйДокумент(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеТекстовогоДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТабличныйДокумент(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ПолеФормы, МенеджерПроекта.Виды().ПолеТабличногоДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппуКнопок(Команда)
	ДобавитьЭлементУправления(МенеджерПроекта.Типы().ГруппаФормы, МенеджерПроекта.Виды().ГруппаКнопок);
	
КонецПроцедуры

//---

&НаКлиенте
Процедура РедактироватьРоли(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПараметрыФормыРолей = Новый Структура;
	ПараметрыФормыРолей.Вставить("РежимРедактированияРолей", Истина);
	ПараметрыФормыРолей.Вставить("ТекущаяРоль", ДанныеВыбора);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("РедактироватьРолиЗавершение", ЭтотОбъект);
	ОткрытьФорму("ВнешняяОбработка.Эскиз.Форма.Роли", ПараметрыФормыРолей, Элементы.Роль,,,, ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьРолиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Роль.СписокВыбора.ЗагрузитьЗначения(РезультатЗакрытия.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПроекта(Команда)
	//Группировки, сортировки, отборы, поиски
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРедактированияСвойств", РежимРедактированияСвойств);
	ПараметрыФормы.Вставить("ЗагружатьСвойстваИзФайла", ЗагружатьСвойстваИзФайла);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("НастройкиПроектаЗавершение", ЭтотОбъект);
	ОткрытьФорму("ВнешняяОбработка.Эскиз.Форма.НастройкиПроекта", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПроектаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗакрытия);
		ОбработатьИзменениеНастроекПроекта();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНастроекПроекта() 
	Элементы.ГруппаКолонокРежимРедактированияСвойств.Видимость = РежимРедактированияСвойств;
	Элементы.ГруппаКнопокРежимРедактированияСвойств.Видимость = РежимРедактированияСвойств;
	
	Если РежимРедактированияСвойств Тогда
		Элементы.СвойстваФормИЭлементовУправления.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Авто;
		Элементы.Свойство.Видимость = Истина;
		Элементы.СвойстваФормИЭлементовУправления.ОтборСтрок = Неопределено;
		
	Иначе		
		Элементы.СвойстваФормИЭлементовУправления.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		Элементы.Свойство.Видимость = Ложь;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокуПрототипаРекурсивно(Элемент) 
	Результат = "";
	
	Родитель = Элемент.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Результат = Элемент;
		
	Иначе
		Результат = ПолучитьСтрокуПрототипаРекурсивно(Родитель);
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ФормыИЭлементыУправленияПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПодключитьОбработчикОжидания("АктивизироватьТекущийЭлемент", 0.3, Истина);
	
	ОпределитьСвойства(ТекущиеДанные);
	ОпределитьГруппуКоманд(ТекущиеДанные);
	
	СкрытьВсеСтраницыКромеТекущей();
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьТекущийЭлемент() Экспорт
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
		// Активизировать форму
	Иначе
		Прототип = ПолучитьСтрокуПрототипаРекурсивно(ТекущиеДанные).Имя;
		МенеджерПроекта.АктивизироватьТекущийЭлемент(Прототип, ТекущиеДанные.Имя);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыОтбораСвойств(ТекущиеДанные) 
	КлючОтбора = "Отбор_";
	
	ПараметрыОтбора = Новый Структура;
	Если ТекущиеДанные = Неопределено Тогда
		КлючОтбора = КлючОтбора + "Пусто";
		
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
		КлючОтбора = КлючОтбора + ТекущиеДанные.Тип;
		
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().ТаблицаФормы Тогда
		КлючОтбора = КлючОтбора + ТекущиеДанные.Тип + "_";
		КлючОтбора = КлючОтбора + ТекущиеДанные.ТипЗначения;
		
	ИначеЕсли Истина
		И ТекущиеДанные.Тип = МенеджерПроекта.Типы().ПолеФормы
		И ТекущиеДанные.Вид = МенеджерПроекта.Виды().ПолеВвода
		Тогда
		
		КлючОтбора = КлючОтбора + ТекущиеДанные.Тип + "_";
		КлючОтбора = КлючОтбора + ТекущиеДанные.Вид + "_";
		КлючОтбора = КлючОтбора + ТекущиеДанные.ТипЗначения;
		
	Иначе
		КлючОтбора = КлючОтбора + ТекущиеДанные.Тип + "_";
		КлючОтбора = КлючОтбора + ТекущиеДанные.Вид;
		
	КонецЕсли;
	
	ПараметрыОтбора.Вставить(КлючОтбора, Истина);
	
	Возврат ПараметрыОтбора;
КонецФункции

&НаКлиенте
Процедура ДополнитьКлючиСвойств(Ключи, ТекущиеДанные) 
	ПараметрыОтбора = ПолучитьПараметрыОтбораСвойств(ТекущиеДанные);
	
	ИскомыеСтроки = СвойстваФормИЭлементовУправления.НайтиСтроки(ПараметрыОтбора);
	Для Каждого Строка Из ИскомыеСтроки Цикл
		Ключи = Ключи + "," + Строка.Свойство;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьСвойства(ТекущиеДанные) Экспорт
	Если РежимРедактированияСвойств Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = ПолучитьПараметрыОтбораСвойств(ТекущиеДанные);
	Элементы.СвойстваФормИЭлементовУправления.ОтборСтрок = Новый ФиксированнаяСтруктура(ПараметрыОтбора);
	
	ИскомыеСтроки = СвойстваФормИЭлементовУправления.НайтиСтроки(ПараметрыОтбора);
	Для Каждого Строка Из ИскомыеСтроки Цикл
		ТекущееЗначение = ТекущиеДанные[Строка.Свойство];
		Строка.Значение = ТекущееЗначение;
		
		ТекущиеДанныеВыбора = Строка.ДанныеВыбора;
		Если ТекущиеДанныеВыбора.Количество() Тогда
			ИскомоеЗначение = Строка.Значение;
			Если ТипЗнч(Строка.Значение) = Тип("Булево") Тогда
				ИскомоеЗначение = Формат(ИскомоеЗначение, "БЛ=Нет; БИ=Да");
				
			КонецЕсли;
			
			ИскомыйЭлемент = ТекущиеДанныеВыбора.НайтиПоЗначению(ИскомоеЗначение);
			Строка.ЗначениеПредставление = ИскомыйЭлемент.Представление;
			
		Иначе
			Строка.ЗначениеПредставление = ТекущееЗначение;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьГруппуКоманд(ТекущиеДанные) 
	Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_Все;
	
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_НачалоРаботы;
		
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().ГруппаФормы Тогда
		Если ТекущиеДанные.Вид = МенеджерПроекта.Виды().Страницы Тогда
			Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_Страницы;
			
		ИначеЕсли Ложь
			Или ТекущиеДанные.Вид = МенеджерПроекта.Виды().КоманднаяПанель
			Или ТекущиеДанные.Вид = МенеджерПроекта.Виды().Подменю
			Или ТекущиеДанные.Вид = МенеджерПроекта.Виды().ГруппаКнопок
			Или ТекущиеДанные.Вид = МенеджерПроекта.Виды().КонтекстноеМеню
			Тогда
			
			Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_КоманднаяПанель;
			
		ИначеЕсли ТекущиеДанные.Вид = МенеджерПроекта.Виды().ГруппаКолонок Тогда
			Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_Таблица;
			
		КонецЕсли;
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().ТаблицаФормы Тогда
		Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_Таблица;
		
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().КнопкаФормы Тогда
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		Если Ложь
			Или Родитель.Вид = МенеджерПроекта.Виды().КоманднаяПанель
			Или Родитель.Вид = МенеджерПроекта.Виды().Подменю
			Или Родитель.Вид = МенеджерПроекта.Виды().ГруппаКнопок
			Или Родитель.Вид = МенеджерПроекта.Виды().КонтекстноеМеню
			Тогда
			
			Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_КоманднаяПанель;
			
		КонецЕсли;
	ИначеЕсли ТекущиеДанные.Тип = МенеджерПроекта.Типы().ПолеФормы Тогда
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		Если Ложь
			Или Родитель.Тип = МенеджерПроекта.Типы().ТаблицаФормы
			Или Родитель.Вид = МенеджерПроекта.Виды().ГруппаКолонок
			Тогда
			
			Элементы.ГруппаКоманды.ТекущаяСтраница = Элементы.ГруппаКоманды_Таблица;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкрытьВсеСтраницыКромеТекущей() 
	ТекущаяСтраница = Элементы.ГруппаКоманды.ТекущаяСтраница;
	
	Страницы = Элементы.ГруппаКоманды.ПодчиненныеЭлементы;
	Для Каждого Страница Из Страницы Цикл
		Страница.Видимость = Страница = ТекущаяСтраница;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПриИзменении(Элемент)
	ТекущиеДанныеПрототип = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанныеПрототип = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеСвойств = Элементы.СвойстваФормИЭлементовУправления.ТекущиеДанные;
	Если ТекущиеДанныеСвойств = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанныеСвойств.Свойство = "Заголовок" Тогда
		ТекущиеДанныеСвойств.Значение = ТекущиеДанныеСвойств.ЗначениеПредставление;
		
	КонецЕсли;
	
	Если ТекущиеДанныеПрототип.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
		Прототип = ТекущиеДанныеПрототип.Имя;
		МенеджерПроекта.ИзменитьСвойство(Прототип, ТекущиеДанныеСвойств.Свойство, ТекущиеДанныеСвойств.Значение);
		
	Иначе
		Прототип = ПолучитьСтрокуПрототипаРекурсивно(ТекущиеДанныеПрототип);
		Если ТекущиеДанныеСвойств.Свойство = "ТипЗначения" Тогда
			МенеджерПроекта.ИзменитьТипЗначения(Прототип.Имя, ТекущиеДанныеСвойств.Свойство, ТекущиеДанныеСвойств.Значение, Ложь, ТекущиеДанныеПрототип.Имя);
			
		Иначе
			МенеджерПроекта.ИзменитьСвойство(Прототип.Имя, ТекущиеДанныеСвойств.Свойство, ТекущиеДанныеСвойств.Значение, Ложь, ТекущиеДанныеПрототип.Имя);
			Если Истина
				И ТекущиеДанныеСвойств.Свойство = "КнопкаПоУмолчанию"
				И ТекущиеДанныеСвойств.Значение = "Да"
				Тогда
				
				Прототип.
				
				ПодключитьОбработчикОжидания("", 1, Истина);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТекущиеДанныеПрототип[ТекущиеДанныеСвойств.Свойство] = ТекущиеДанныеСвойств.Значение;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСледующуюСтроку(ТекущиеДанные) 
	Результат = Неопределено;
	
	#Если _ Тогда
		ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	#КонецЕсли
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		СтрокиУровня = ФормыИЭлементыУправления.ПолучитьЭлементы();
		
	Иначе
		СтрокиУровня = Родитель.ПолучитьЭлементы();
		
	КонецЕсли;
	
	ВзятьСледующий = Ложь;
	
	Для Каждого Строка Из СтрокиУровня Цикл
		Если ВзятьСледующий Тогда
			Результат = Строка;
			
			Прервать;
		ИначеЕсли Строка.Имя = ТекущиеДанные.Имя Тогда
			ВзятьСледующий = Не Строка.Перемещается;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;    
КонецФункции

&НаКлиенте
Функция ПолучитьПредыдущуюСтроку(ТекущиеДанные) 
	Результат = Неопределено;
	//
	//#Если _ Тогда
	//	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	//#КонецЕсли
	//
	//Родитель = ТекущиеДанные.ПолучитьРодителя();
	//Если Родитель = Неопределено Тогда
	//	Возврат Результат;
	//КонецЕсли;
	//
	//СтрокиУровня = Родитель.ПолучитьЭлементы();
	//Для Каждого Строка Из СтрокиУровня Цикл
	//	Если ВзятьСледующий Тогда
	//		Результат = Строка.Заголовок;
	//		
	//		Прервать;
	//	Если Строка.Имя = ТекущиеДанные.Имя Тогда
	//		ВзятьСледующий = Истина;
	//		
	//	КонецЕсли;
	//КонецЦикла;
	//
	Возврат Результат;    
КонецФункции

&НаКлиенте
Функция ДобавитьСтрокуВставкой(ТекущиеДанные) 
	Результат = Неопределено;
	
	СтрокиУровня = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	Для Каждого Строка Из СтрокиУровня Цикл
		Если Строка.Имя = ТекущиеДанные.Имя Тогда
			ИндексВставки = СтрокиУровня.Индекс(Строка) + 1;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Результат = СтрокиУровня.Вставить(ИндексВставки);
	
	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура ДобавитьЭлементУправления(Тип, Вид = "", ТипЗначения = "", ЭтоТаблица = Ложь)
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если Ложь
		Или ТекущиеДанные.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения
		Или ТекущиеДанные.Тип = МенеджерПроекта.Типы().ГруппаФормы
		Или ТекущиеДанные.Тип = МенеджерПроекта.Типы().ТаблицаФормы
		Тогда
		
		НоваяСтрока = ТекущиеДанные.ПолучитьЭлементы().Добавить();
		Родитель = ТекущиеДанные;
		
	Иначе
		НоваяСтрока = ДобавитьСтрокуВставкой(ТекущиеДанные);
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		
	КонецЕсли;
	
	НоваяСтрока.Тип = Тип;
	НоваяСтрока.Вид = Вид;
	
	Если Родитель.Тип <> МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
		НоваяСтрока.Родитель = Родитель.Имя;
		
	КонецЕсли;
	
	НоваяСтрока.Прототип = ПолучитьСтрокуПрототипаРекурсивно(Родитель).Имя;
	
	Если Тип = МенеджерПроекта.Типы().ГруппаФормы Тогда
		Связать = Ложь;
		Ключи = "Имя,Тип,Вид,Родитель,Прототип";
		
		Если Вид = МенеджерПроекта.Виды().ОбычнаяГруппа Тогда
			НоваяСтрока.ИндексКартинки = 2;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Группа");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Группировка = "Вертикальная";
			НоваяСтрока.ОтображатьЗаголовок = Истина;

		ИначеЕсли Вид = МенеджерПроекта.Виды().КоманднаяПанель Тогда
			НоваяСтрока.ИндексКартинки = 1;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("КоманднаяПанель");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().Страницы Тогда
			НоваяСтрока.ИндексКартинки = 19;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Страницы");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().Страница Тогда
			НоваяСтрока.ИндексКартинки = 18;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Страница");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Группировка = "Вертикальная";

		ИначеЕсли Вид = МенеджерПроекта.Виды().Подменю Тогда
			НоваяСтрока.ИндексКартинки = 16;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Подменю");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().ГруппаКнопок Тогда
			НоваяСтрока.ИндексКартинки = 9;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ГруппаКнопок");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.ОтображениеГруппыКнопок = "Авто";
			
		ИначеЕсли Вид = МенеджерПроекта.Виды().ГруппаКолонок Тогда
			НоваяСтрока.ИндексКартинки = 10;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ГруппаКолонок");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().КонтекстноеМеню Тогда
			НоваяСтрока.ИндексКартинки = 27;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("КонтекстноеМеню");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		КонецЕсли;
	ИначеЕсли Тип = МенеджерПроекта.Типы().ПолеФормы Тогда
		Связать = Истина;
		Ключи = "Имя,Тип,Вид,Родитель,Прототип,Путь,ПутьКДанным";
		
		Если Вид = МенеджерПроекта.Виды().ПолеВвода Тогда
			Если ЭтоТаблица Тогда
				НоваяСтрока.ИндексКартинки = 14;
				
				НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Колонка");
				НоваяСтрока.Имя = НовоеИмя;
				НоваяСтрока.ПутьКДанным = НовоеИмя;
				НоваяСтрока.Заголовок = НовоеИмя;
				НоваяСтрока.ТипЗначения = "Строка";
				НоваяСтрока.Путь = "";

			Иначе
				НоваяСтрока.ИндексКартинки = 3;
				
				НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Поле");
				НоваяСтрока.Имя = НовоеИмя;
				НоваяСтрока.ПутьКДанным = НовоеИмя;
				НоваяСтрока.Заголовок = НовоеИмя;
				НоваяСтрока.ТипЗначения = "Строка";
				НоваяСтрока.Путь = "";

			КонецЕсли;
		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеФорматированногоДокумента Тогда
			НоваяСтрока.ИндексКартинки = 23;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ФорматированныйДокумент");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.ПутьКДанным = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Путь = "";

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеНадписи Тогда
			// Только для форматированного документа, остальное - Надпись)
			
			НоваяСтрока.ИндексКартинки = 26;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ФорматированнаяСтрока");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.ПутьКДанным = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Путь = "";

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеТекстовогоДокумента Тогда
			НоваяСтрока.ИндексКартинки = 24;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ТекстовыйДокумент");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.ПутьКДанным = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Путь = "";

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеТабличногоДокумента Тогда
			НоваяСтрока.ИндексКартинки = 25;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ТабличныйДокумент");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.ПутьКДанным = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.Путь = "";

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеФлажка Тогда
			Ключи = Ключи + ",ТипЗначения";
			
			НоваяСтрока.ИндексКартинки = 20;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Флажок");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.ПутьКДанным = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			НоваяСтрока.ТипЗначения = "Булево";

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеПереключателя Тогда
			НоваяСтрока.ИндексКартинки = 15;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Переключатель");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеИндикатора Тогда
			НоваяСтрока.ИндексКартинки = 11;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Индикатор");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеПолосыРегулирования Тогда
			НоваяСтрока.ИндексКартинки = 17;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("ПолосаРегулирования");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().ПолеКалендаря Тогда
			НоваяСтрока.ИндексКартинки = 12;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Календарь");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		КонецЕсли;
	ИначеЕсли Тип = МенеджерПроекта.Типы().КнопкаФормы Тогда
		Связать = Истина;
		Ключи = "Имя,Тип,Вид,Родитель,Прототип";
		
		Если Вид = МенеджерПроекта.Виды().ОбычнаяКнопка Тогда
			НоваяСтрока.ИндексКартинки = 4;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Кнопка");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().КнопкаКоманднойПанели Тогда
			НоваяСтрока.ИндексКартинки = 4;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Кнопка");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().Гиперссылка Тогда
			НоваяСтрока.ИндексКартинки = 21;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Гиперссылка");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().ГиперссылкаКоманднойПанели Тогда
			НоваяСтрока.ИндексКартинки = 21;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Гиперссылка");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		КонецЕсли;
	ИначеЕсли Тип = МенеджерПроекта.Типы().ТаблицаФормы Тогда
		Связать = Истина;
		Ключи = "";
		
		Если ТипЗначения = МенеджерПроекта.ТипыЗначения().ТаблицаЗначений Тогда
			НоваяСтрока.ИндексКартинки = 7;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Таблица");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;
			
			НоваяСтрока.Тип = МенеджерПроекта.Типы().ТаблицаФормы;

		ИначеЕсли ТипЗначения = МенеджерПроекта.ТипыЗначения().ДеревоЗначений Тогда
			НоваяСтрока.ИндексКартинки = 8;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Дерево");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		КонецЕсли;
	ИначеЕсли Тип = МенеджерПроекта.Типы().ДекорацияФормы Тогда
		Связать = Ложь;
		Ключи = "";
		
		Если Вид = МенеджерПроекта.Виды().Надпись Тогда
			НоваяСтрока.ИндексКартинки = 5;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Надпись");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		ИначеЕсли Вид = МенеджерПроекта.Виды().Картинка Тогда
			НоваяСтрока.ИндексКартинки = 13;
			
			НовоеИмя = МенеджерПроекта.ПолучитьНовоеИмя("Картинка");
			НоваяСтрока.Имя = НовоеИмя;
			НоваяСтрока.Заголовок = НовоеИмя;

		КонецЕсли;
	КонецЕсли;
	
	ДополнитьКлючиСвойств(Ключи, НоваяСтрока);
	СвойстваЭлементаУправления = Новый Структура(Ключи);
	ЗаполнитьЗначенияСвойств(СвойстваЭлементаУправления, НоваяСтрока);
	
	Попытка
		МенеджерПроекта.ДобавитьЭлементУправления(СвойстваЭлементаУправления, Связать, Ложь);
		
	Исключение
		//
		
	КонецПопытки;
	
	Элементы.ФормыИЭлементыУправления.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьПодсказокКоманд(Команда)
	ОпределитьВидимостьПодсказокКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьВидимостьПодсказокКоманд(ЭтоИзменение = Истина) 
	Если ЭтоИзменение Тогда
		ПоказыватьПодсказкиКоманд = Не ПоказыватьПодсказкиКоманд;
		
	КонецЕсли;
	
	Если ПоказыватьПодсказкиКоманд Тогда
		ВариантОтображенияПодсказки = ОтображениеПодсказки.ОтображатьСправа;
		
	Иначе
		ВариантОтображенияПодсказки = ОтображениеПодсказки.Нет;
		
	КонецЕсли;
	
	Страницы = Элементы.ГруппаКоманды.ПодчиненныеЭлементы;
	Для Каждого Страница Из Страницы Цикл
		ЭлементыСтраницы = Страница.ПодчиненныеЭлементы;
		Для Каждого ЭлементСтраницы Из ЭлементыСтраницы Цикл
			Если Истина
				И ТипЗнч(ЭлементСтраницы) = Тип("КнопкаФормы")
				И ЭлементСтраницы.ИмяКоманды <> "ВидимостьПодсказокКоманд"
				Тогда
				
				ЭлементСтраницы.ОтображениеПодсказки = ВариантОтображенияПодсказки;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СвойстваФормИЭлементовУправленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если РежимРедактированияСвойств Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СвойстваФормИЭлементовУправления.ТекущиеДанные;
	Если Элемент.ТекущийЭлемент.Имя = "Свойство" Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ДанныеВыбора.Количество() Тогда
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СвойстваФормИЭлементовУправленияПриАктивизацииЯчейкиЗавершение", ЭтотОбъект);
		ПоказатьВыборИзСписка(ОписаниеОповещения, ТекущиеДанные.ДанныеВыбора, Элементы.СвойстваФормИЭлементовУправления.ТекущийЭлемент);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СвойстваФормИЭлементовУправленияПриАктивизацииЯчейкиЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	ТекущиеДанные = Элементы.СвойстваФормИЭлементовУправления.ТекущиеДанные;
	Если Не ВыбранныйЭлемент = Неопределено Тогда
		ТекущиеДанные.Значение = ВыбранныйЭлемент.Значение;
		ТекущиеДанные.ЗначениеПредставление = ВыбранныйЭлемент.Представление;
		
		ЗначениеПриИзменении(Неопределено);
		ОпределитьСвойства(Элементы.ФормыИЭлементыУправления.ТекущиеДанные); //todo
		
	КонецЕсли;
	
	Элементы.СвойстваФормИЭлементовУправления.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСвойстваВФайл(Команда)
	Результат = СохранитьСвойстваВФайлНаСервере();
	НачатьПолучениеФайлаССервера(, Результат.Адрес, Результат.ПутьКФайлу);
	
КонецПроцедуры

&НаСервере
Функция СохранитьСвойстваВФайлНаСервере()
	Результат = Новый Структура("Адрес,ПутьКФайлу", "", "");
	Результат.ПутьКФайлу = СтрЗаменить(РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла, "Эскиз.epf", "Свойства.json");
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	СвойстваНаСервере = РеквизитФормыВЗначение("СвойстваФормИЭлементовУправления");
	Json = СериализоватьВJSON(ТаблицаСвойствВМассивСтруктур(СвойстваНаСервере, Истина));
	
	Писатель = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаТекста.UTF8);
	Писатель.Записать(Json);
	Писатель.Закрыть();
	
	Файл = Новый Файл(ИмяВременногоФайла);
	Если Файл.Существует() Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		Результат.Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ТаблицаСвойствВМассивСтруктур(Знач ТаблицаЗначений, ВсеКолонки, ИменаКолонок = "")
	Результат = Новый Массив;
	
	Если ВсеКолонки Тогда
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			ИменаКолонок = ИменаКолонок + "," + Колонка.Имя;
		КонецЦикла;
		
		ИменаКолонок = Сред(ИменаКолонок, 2);
	КонецЕсли;
	
	Для Каждого Строка Из ТаблицаЗначений Цикл
		СтруктураСтроки = Новый Структура(ИменаКолонок);
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
		Результат.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура МассивСтруктурВТаблицуСвойств(ТаблицаЗначений, Свойства, ВсеКолонки, ИменаКолонок = "")
	Результат = Новый Массив;
	
	ТаблицаЗначений.Очистить();
	
	Если ВсеКолонки Тогда
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			ИменаКолонок = ИменаКолонок + "," + Колонка.Имя;
		КонецЦикла;
		
		ИменаКолонок = Сред(ИменаКолонок, 2);
	КонецЕсли;
	
	ИменаДанныхВыбора = Новый Массив;
	ИменаДанныхВыбора.Добавить("ДанныеВыбора");
	ИменаДанныхВыбора.Добавить("ДанныеВыбора_Родитель_Подменю");
	ИменаДанныхВыбора.Добавить("ДанныеВыбора_Родитель_Страницы");
	ИменаДанныхВыбора.Добавить("ДанныеВыбора_Родитель_КоманднаяПанель");
	
	Для Каждого Свойство Из Свойства Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Свойство);
		
		Для Каждого ИмяДанныхВыбора Из ИменаДанныхВыбора Цикл
			Для Каждого Строка Из Свойство[ИмяДанныхВыбора] Цикл
				НоваяСтрока[ИмяДанныхВыбора].Добавить(Строка.Ключ, Строка.Значение);
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СериализоватьВJSON(Значение) Экспорт
	Писатель = Новый ЗаписьJSON;
	Писатель.ПроверятьСтруктуру = Истина;
	
	ПараметрыПисателя = Новый ПараметрыЗаписиJSON(, Символы.Таб,, ЭкранированиеСимволовJSON.СимволыВнеBMP);
	Писатель.УстановитьСтроку(ПараметрыПисателя);
	
	НастройкаСериализации = Новый НастройкиСериализацииJSON;
	НастройкаСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкаСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(Писатель, Значение, НастройкаСериализации, "Преобразователь", ЭтотОбъект);
	
	Результат = Писатель.Закрыть();
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция Преобразователь(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	Результат = Новый Структура;
	Для Каждого Элемент Из Значение Цикл
		Результат.Вставить(Элемент.Значение, Элемент.Представление);
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДесериализоватьИзJSON(JSON, ИзФайла = Ложь) Экспорт
	Результат = Неопределено;
	
	Читатель = Новый ЧтениеJSON;
	
	Если ИзФайла Тогда
		Читатель.ОткрытьФайл(JSON);
		
	Иначе
		Читатель.УстановитьСтроку(JSON);
		
	КонецЕсли;
	
	ИменаСвойствСоЗначениемТипаДата = Новый Массив;
	//ИменаСвойствСоЗначениемТипаДата.Добавить("");
	
	Результат = ПрочитатьJSON(Читатель,, ИменаСвойствСоЗначениемТипаДата, ФорматДатыJSON.ISO);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьСвойстваИзФайла(Команда)
	ВыполнитьЗагрузкуСвойствИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуСвойствИзМакета() 
	ВыполнитьЗагрузкуСвойствИзМакетаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуСвойствИзМакетаНаСервере() 
	СвойстваФормИЭлементовУправленияНаСервере = РеквизитФормыВЗначение("СвойстваФормИЭлементовУправления");
	
	Json = ПолучитьJsonИзМакета("Свойства");
	Свойства = ДесериализоватьИзJSON(Json);
	МассивСтруктурВТаблицуСвойств(СвойстваФормИЭлементовУправленияНаСервере, Свойства, Истина);
	
	ЗначениеВРеквизитФормы(СвойстваФормИЭлементовУправленияНаСервере, "СвойстваФормИЭлементовУправления");
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуСвойствИзФайла() 
	ПутьКФайлу = "";
	ЗагрузитьСвойстваИзФайлаНаСервере(ПутьКФайлу);
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗагрузитьСвойстваИзФайлаЗавершение", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ОписаниеОповещенияОЗавершении,,,, ПутьКФайлу);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьJsonИзМакета(ИмяМакета) 
	Результат = РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета).ПолучитьТекст();	
	
	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура ЗагрузитьСвойстваИзФайлаЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	ЗагрузитьСвойстваИзФайлаЗавершениеНаСервере(ОписаниеПомещенногоФайла.Адрес);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСвойстваИзФайлаЗавершениеНаСервере(Адрес) 
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	СвойстваФормИЭлементовУправленияНаСервере = РеквизитФормыВЗначение("СвойстваФормИЭлементовУправления");
	
	Свойства = ДесериализоватьИзJSON(ИмяВременногоФайла, Истина);
	МассивСтруктурВТаблицуСвойств(СвойстваФормИЭлементовУправленияНаСервере, Свойства, Истина);
	
	ЗначениеВРеквизитФормы(СвойстваФормИЭлементовУправленияНаСервере, "СвойстваФормИЭлементовУправления");
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьСвойстваИзФайлаНаСервере(ПутьКФайлу)
	ПутьКФайлу = СтрЗаменить(РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла, "Эскиз.epf", "Свойства.json");
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваФормИЭлементовУправленияДанныеВыбораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	РедактированиеДанныхВыбора("ДанныеВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеДанныхВыбора(ИмяКолонкиДанынхВыбора) 
	ТекущиеДанные = Элементы.СвойстваФормИЭлементовУправления.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДанныеВыбора", ТекущиеДанные[ИмяКолонкиДанынхВыбора]);
	
	ДополнительнаеПараметрыФормы = Новый Структура;
	ДополнительнаеПараметрыФормы.Вставить("ИмяКолонкиДанынхВыбора", ИмяКолонкиДанынхВыбора);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("РедактированиеДанныхВыбораЗавершение", ЭтотОбъект, ДополнительнаеПараметрыФормы);
	ОткрытьФорму("ВнешняяОбработка.Эскиз.Форма.ДанныеВыбораСвойства", ПараметрыФормы, ЭтаФорма,,,, ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеДанныхВыбораЗавершение(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	Если ДанныеВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СвойстваФормИЭлементовУправления.ТекущиеДанные;
	ТекущиеДанные[ДополнительныеПараметры.ИмяКолонкиДанынхВыбора].Очистить();
	
	Для Каждого Строка Из ДанныеВыбора Цикл
		ТекущиеДанные[ДополнительныеПараметры.ИмяКолонкиДанынхВыбора].Добавить(Строка.Значение, Строка.Представление);
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ФормыИЭлементыУправленияОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Для Каждого СтрокаИсточник Из ПараметрыПеретаскивания.Значение Цикл
		Родитель = СтрокаИсточник.ПолучитьРодителя();
		Если Родитель = Неопределено Тогда
			СтрокиУровня = ФормыИЭлементыУправления.ПолучитьЭлементы();
			
		Иначе
			СтрокиУровня = Родитель.ПолучитьЭлементы();
			
		КонецЕсли;
		
		СтрокиУровня.Удалить(СтрокаИсточник);
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ФормыИЭлементыУправленияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	
	Если ПеремещениеВозможно() Тогда
		Перемещенные = Новый Массив;
		ВыполнитьПеремещение(ПараметрыПеретаскивания.Значение, Строка, Перемещенные, Истина);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПеремещениеВозможно() 
	Результат = Истина;
	
	//todo	
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыполнитьПеремещение(ПеремещаемыеЭлементы, ИдентификаторСтрокиПриемника, Перемещенные, Знач ПерваяИтерация = Ложь)
	СтрокаПриемник = ФормыИЭлементыУправления.НайтиПоИдентификатору(ИдентификаторСтрокиПриемника);
	ЭтоГруппа = Ложь
	Или СтрокаПриемник.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения
	Или СтрокаПриемник.Тип = МенеджерПроекта.Типы().ГруппаФормы
	Или СтрокаПриемник.Тип = МенеджерПроекта.Типы().ТаблицаФормы;
	
	Родитель = СтрокаПриемник.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		СтрокиУровня = ФормыИЭлементыУправления.ПолучитьЭлементы();
		
	Иначе
		СтрокиУровня = Родитель.ПолучитьЭлементы();
		
	КонецЕсли;
	
	РасположениеСтрокиПриемника = СтрокиУровня.Индекс(СтрокаПриемник) + 1;
	ЭтоПоследнийЭлементУровня = СтрокиУровня.Количество() = РасположениеСтрокиПриемника;
	
	Для Каждого СтрокаИсточник Из ПеремещаемыеЭлементы Цикл
		Если ТипЗнч(СтрокаИсточник) = Тип("Число") Тогда
			СтрокаИсточник = ФормыИЭлементыУправления.НайтиПоИдентификатору(СтрокаИсточник);	
			
		КонецЕсли;
		
		Если ПерваяИтерация И ИерархияНеКорректна(СтрокаПриемник, СтрокаИсточник) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Перемещенные.Найти(СтрокаИсточник.Имя) = Неопределено Тогда
			НовыйЭлемент = Неопределено;
			Если ЭтоГруппа Тогда
				НовыйЭлемент = СтрокаПриемник.ПолучитьЭлементы().Добавить();
				
			ИначеЕсли ЭтоПоследнийЭлементУровня Тогда
				НовыйЭлемент = СтрокиУровня.Добавить();
				
			Иначе
				НовыйЭлемент = СтрокиУровня.Вставить(РасположениеСтрокиПриемника);
				
			КонецЕсли;
			
			Если НовыйЭлемент <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтрокаИсточник);	
				СтрокаИсточник.Перемещается = Истина;
				
				ПрототипИсточник = ПолучитьСтрокуПрототипаРекурсивно(СтрокаИсточник).Имя;
				
				НовыйЭлемент.Прототип = ПолучитьСтрокуПрототипаРекурсивно(НовыйЭлемент).Имя;
				
				Родитель = НовыйЭлемент.ПолучитьРодителя();
				Если Родитель.Тип = МенеджерПроекта.Типы().ФормаКлиентскогоПриложения Тогда
					НовыйЭлемент.Родитель = "";
					
				Иначе
					НовыйЭлемент.Родитель = Родитель.Имя;
					
				КонецЕсли;
				
				Если ПрототипИсточник = НовыйЭлемент.Прототип И ПерваяИтерация Тогда
					ДанныеПеремещения = Новый Структура;
					ДанныеПеремещения.Вставить("ИмяПрототипа", НовыйЭлемент.Прототип);
					
					СледующаяСтрока = ПолучитьСледующуюСтроку(НовыйЭлемент);
					Если СледующаяСтрока = Неопределено Тогда
						ИмяЭлементаМесторасположения = "";
						
					Иначе
						ИмяЭлементаМесторасположения = СледующаяСтрока.Имя;
						
					КонецЕсли;
					
					ДанныеПеремещения.Вставить("ИмяЭлементаМесторасположения", ИмяЭлементаМесторасположения);
					ДанныеПеремещения.Вставить("ИмяРодительскогоЭлемента", НовыйЭлемент.Родитель);
					ДанныеПеремещения.Вставить("ИмяПеремещаемогоЭлемента", НовыйЭлемент.Имя);
					
					МенеджерПроекта.ПереместитьЭлементУправления(ДанныеПеремещения);
					
				Иначе
					//todo
					
				КонецЕсли;
				
				ДочерниеЭлементы = СтрокаИсточник.ПолучитьЭлементы();
				Если ДочерниеЭлементы.Количество() Тогда
					ВыполнитьПеремещение(ДочерниеЭлементы, НовыйЭлемент.ПолучитьИдентификатор(), Перемещенные);
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ИерархияНеКорректна(СтрокаПриемник, СтрокаИсточник)
	Результат = Ложь;
	
	Если СтрокаПриемник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаПриемник.Вид = МенеджерПроекта.Виды().Страницы Тогда
		Если СтрокаИсточник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().Страница Тогда
			Результат = Ложь;
			
		Иначе 
			Результат = Истина;
			
		КонецЕсли;
	ИначеЕсли СтрокаПриемник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаПриемник.Вид = МенеджерПроекта.Виды().КоманднаяПанель Тогда
		Если СтрокаИсточник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().Подменю Тогда
			Результат = Ложь;
			
		ИначеЕсли СтрокаИсточник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().ГруппаКнопок Тогда
			Результат = Ложь;
			
		ИначеЕсли СтрокаИсточник.Тип = МенеджерПроекта.Типы().КнопкаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().КнопкаКоманднойПанели Тогда
			Результат = Ложь;
			
		ИначеЕсли СтрокаИсточник.Тип = МенеджерПроекта.Типы().КнопкаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().ГиперссылкаКоманднойПанели Тогда
			Результат = Ложь;
			
		Иначе 
			Результат = Истина;
			
		КонецЕсли;
	ИначеЕсли СтрокаПриемник.Тип = МенеджерПроекта.Типы().ТаблицаФормы Тогда
		Если СтрокаИсточник.Тип = МенеджерПроекта.Типы().ПолеФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().ПолеВвода Тогда
			Результат = Ложь;
			
		ИначеЕсли СтрокаИсточник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().ГруппаКолонок Тогда
			Результат = Ложь;
			
		ИначеЕсли СтрокаИсточник.Тип = МенеджерПроекта.Типы().ГруппаФормы И СтрокаИсточник.Вид = МенеджерПроекта.Виды().КонтекстноеМеню Тогда
			Результат = Ложь;
			
		Иначе 
			Результат = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура ПереместитьВверх(Команда)
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиУровня = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	ИндексТекущейСтроки = СтрокиУровня.Индекс(ТекущиеДанные);
	ИндексПредыдущейСтроки = ИндексТекущейСтроки - 1;
	
	Если ИндексТекущейСтроки > 0 Тогда
		ДанныеПеремещения = Новый Структура;
		ДанныеПеремещения.Вставить("ИмяПрототипа", ТекущиеДанные.Прототип);
		ДанныеПеремещения.Вставить("ИмяПеремещаемогоЭлемента", ТекущиеДанные.Имя);
		ДанныеПеремещения.Вставить("ИмяРодительскогоЭлемента", ТекущиеДанные.Родитель);
		
		ПредыдущиеДанные = СтрокиУровня[ИндексПредыдущейСтроки];
		ДанныеПеремещения.Вставить("ИмяЭлементаМесторасположения", ПредыдущиеДанные.Имя);
		
		МенеджерПроекта.ПереместитьЭлементУправления(ДанныеПеремещения);
		
		СтрокиУровня.Сдвинуть(ИндексТекущейСтроки, -1);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	ТекущиеДанные = Элементы.ФормыИЭлементыУправления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиУровня = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	ИндексТекущейСтроки = СтрокиУровня.Индекс(ТекущиеДанные);
	ИндексСледующейСтроки = ИндексТекущейСтроки + 1;
	ВсегоСтрокУровня = СтрокиУровня.Количество() - 1;
	
	Если ИндексТекущейСтроки < ВсегоСтрокУровня Тогда
		ДанныеПеремещения = Новый Структура;
		ДанныеПеремещения.Вставить("ИмяПрототипа", ТекущиеДанные.Прототип);
		ДанныеПеремещения.Вставить("ИмяЭлементаМесторасположения", ТекущиеДанные.Имя);
		ДанныеПеремещения.Вставить("ИмяРодительскогоЭлемента", ТекущиеДанные.Родитель);
		
		СледующиеДанные = СтрокиУровня[ИндексСледующейСтроки];
		ДанныеПеремещения.Вставить("ИмяПеремещаемогоЭлемента", СледующиеДанные.Имя);
		
		МенеджерПроекта.ПереместитьЭлементУправления(ДанныеПеремещения);
		
		СтрокиУровня.Сдвинуть(ИндексТекущейСтроки, 1);
		
	КонецЕсли;
КонецПроцедуры
