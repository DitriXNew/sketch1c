&НаСервере
Функция СоздатьЭлементФормы(ЭлементРодитель,ИмяЭлемента,ПутьКДанным,Вид,ТолькоПросмотр)

    ЭлементФормы = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), ЭлементРодитель);
    ЭлементФормы.ПутьКДанным = ПутьКДанным; 
    ЭлементФормы.Вид         = Вид;
    ЭлементФормы.ТолькоПросмотр = ТолькоПросмотр;

КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    // Создадим группу для стандартных реквизитов
    ГрпСтандартные = Элементы.Добавить("ГруппаСтандартные",Тип("ГруппаФормы"),ЭтаФорма);
    ГрпСтандартные.Вид = ВидГруппыФормы.ОбычнаяГруппа;
    ГрпСтандартные.ОтображатьЗаголовок = Ложь;
    ГрпСтандартные.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;

    // Выведем на форму реквизиты: Код, Наименование
    СоздатьЭлементФормы(ГрпСтандартные,"Код","Объект.Код",ВидПоляФормы.ПолеНадписи,Истина);
    СоздатьЭлементФормы(ГрпСтандартные,"Наименование","Объект.Наименование",ВидПоляФормы.ПолеВвода,Ложь);

    СтруктураСправочника = Объект.Ссылка.Метаданные().Реквизиты;

    // Создадим группу для реквизитов справочника. Группа сворачиваемая.
    ГрпПользовательские = Элементы.Добавить("ГруппаПользовательские",Тип("ГруппаФормы"),ЭтаФорма);
    ГрпПользовательские.Вид = ВидГруппыФормы.ОбычнаяГруппа;
    ГрпПользовательские.Заголовок = "Пользовательские реквизиты";
    ГрпПользовательские.Поведение    = ПоведениеОбычнойГруппы.Свертываемая;

    // Добавим на форму реквизиты справочника
    Для Каждого Реквизит из СтруктураСправочника Цикл 

        СоздатьЭлементФормы(ГрпПользовательские,
                            Реквизит.Имя,
                            "Объект."+Реквизит.Имя,
                            ВидПоляФормы.ПолеВвода,
                            Ложь);

    КонецЦикла;

    // Выведем курс основной валюты расчетов

    // Создадим группу для курса валюты
    ГруппаКурсВалюты = Элементы.Добавить("ГруппаКурс",Тип("ГруппаФормы"),ЭтаФорма);
    ГруппаКурсВалюты.Вид = ВидГруппыФормы.ОбычнаяГруппа;
    ГруппаКурсВалюты.Заголовок = "Курс";

    // Получим последний курс валюты
    Данные = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ТекущаяДата(), 
                                                            Новый Структура("Валюта", 
                                                            Объект.ОсновнаяВалютаРасчетов));
    ОбъектМетаданных = Метаданные.Регистрысведений.КурсыВалют;

    // Добавим реквизиты и элементы форм
    Для Каждого Элемент из Данные Цикл 

        // Добавляем реквизиты
        ДобавляемыеРеквизиты = Новый Массив;
        МассивТипов = новый Массив;
        МассивТипов.Добавить(ТипЗнч(Элемент.Значение));
        Реквизит = Новый РеквизитФормы("КурсыВалют" + Элемент.Ключ,
                                        Новый ОписаниеТипов(МассивТипов),, 
                                        ОбъектМетаданных.Ресурсы[Элемент.Ключ], 
                                        Истина);

        ДобавляемыеРеквизиты.Добавить(Реквизит);
        ИзменитьРеквизиты(ДобавляемыеРеквизиты);

        // Добавляем элементы форм
        СоздатьЭлементФормы(ГруппаКурсВалюты,
                            Элемент.Ключ,
                            "КурсыВалют" + Элемент.Ключ,
                            ВидПоляФормы.ПолеВвода,
                            Истина);

        // Устанавливаем значение реквизиту формы
        ЭтаФорма["КурсыВалют" + Элемент.Ключ] = Элемент.Значение;

    КонецЦикла;   

    // Таблица курсов

    // Добавляем реквизит Динамический список
    ДобавляемыеРеквизиты = Новый Массив;
    МассивТипов = новый Массив;
    МассивТипов.Добавить(Тип("ДинамическийСписок"));
    Реквизит = Новый РеквизитФормы("СписокКурсыВалют",новый ОписаниеТипов(МассивТипов),,, Истина);

    ДобавляемыеРеквизиты.Добавить(Реквизит);
    ИзменитьРеквизиты(ДобавляемыеРеквизиты);

    // Выводим Динамический список на форму
    ЭлементФормы = Элементы.Добавить("СписокКурсыВалют", Тип("ТаблицаФормы"), ЭтаФорма);

    // Устанавливаем параметры динамического списка
    ЭлементДинСписок = ЭтаФорма["СписокКурсыВалют"];
    ЭлементДинСписок.ОсновнаяТаблица = "РегистрСведений.КурсыВалют";

    ТаблицаФормы = Элементы.Добавить("МояНоваяТаблица", Тип("ТаблицаФормы"));
    ТаблицаФормы.ПутьКДанным = "СписокКурсыВалют";

    // Создаем элементы формы для реквизитов Динамического списка
    СоздатьЭлементФормы(ТаблицаФормы,"КолонкаВалюта","СписокКурсыВалют.Валюта",ВидПоляФормы.ПолеВвода,Ложь); 
    СоздатьЭлементФормы(ТаблицаФормы,"КолонкаКурс","СписокКурсыВалют.Курс",ВидПоляФормы.ПолеВвода,Ложь); 
    СоздатьЭлементФормы(ТаблицаФормы,"КолонкаПериод","СписокКурсыВалют.Период",ВидПоляФормы.ПолеВвода,Ложь); 

КонецПроцедуры
